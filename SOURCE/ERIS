      *-----------------------                                                  
       IDENTIFICATION DIVISION.                                                 
      *-----------------------                                                  
       PROGRAM-ID.   ERIS.                                                      
       AUTHOR.       BENJAMIN THOMPSON.                                         
       DATE-WRITTEN. 19/10/21.                                                  
                                                                                
      *--------------------                                                     
       ENVIRONMENT DIVISION.                                                    
      *--------------------                                                     
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT ERIS-PERSON-FILE   ASSIGN TO UT-S-ERISPER                     
           ORGANIZATION IS SEQUENTIAL.                                          
           SELECT ERIS-PROVIDER-FILE ASSIGN TO UT-S-ERISPRO                     
           ORGANIZATION IS SEQUENTIAL.                                          
           SELECT ERIS-VACCINE-FILE  ASSIGN TO UT-S-ERISVAC                     
           ORGANIZATION IS SEQUENTIAL.                                          
           SELECT ERIS-PARM-FILE     ASSIGN TO UT-S-ERISPRM                     
           ORGANIZATION IS SEQUENTIAL.                                          
                                                                                
      *-------------                                                            
       DATA DIVISION.                                                           
      *-------------                                                            
       FILE SECTION.                                                            
       FD  ERIS-PERSON-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 130 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS ERIS-PERSON-REC.                                      
       01  ERIS-PERSON-REC                PIC X(130).                           
                                                                                
       FD  ERIS-PROVIDER-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 130 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS ERIS-PROVIDER-REC.                                    
       01  ERIS-PROVIDER-REC              PIC X(130).                           
                                                                                
       FD  ERIS-VACCINE-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 130 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS ERIS-VACCINE-REC.                                     
       01  ERIS-VACCINE-REC               PIC X(130).                           
                                                                                
       FD  ERIS-PARM-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS ERIS-PARM-REC.                                        
       01  ERIS-PARM-REC                  PIC X(80).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
      * End of File switch                                                      
       77 ERIS-PERSON-EOF                 PIC X(01) VALUE SPACE.                
          88 PERSON-EOF                   VALUE 'Y'.                            
       77 ERIS-PROVIDER-EOF               PIC X(01) VALUE SPACE.                
          88 PROVIDER-EOF                 VALUE 'Y'.                            
       77 ERIS-VACCINE-EOF                PIC X(01) VALUE SPACE.                
          88 VACCINE-EOF                  VALUE 'Y'.                            
       77 ERIS-PARM-EOF                   PIC X(01) VALUE SPACE.                
          88 PARM-EOF                     VALUE 'Y'.                            
                                                                                
      * Db2 return code                                                         
       77 SQLCODE-OUT                     PIC S9(03) VALUE 0.                   
                                                                                
      * Linkage variables                                                       
       01 LK-DELAY.                                                             
          05 LK-DELAY-LOW                 PIC S9(09) BINARY.                    
          05 LK-DELAY-HIGH                PIC S9(09) BINARY.                    
          05 LK-DELAY-RANDOM              PIC S9(09) BINARY.                    
          05 LK-DELAY-DYNAM-FUNC          PIC X(08).                            
                                                                                
      * Working Storage variables                                               
       01 WS-POSTCODE-STATE.                                                    
          05 WS-POSTCODE                  PIC 9(04).                            
          05 WS-STATE                     PIC X(03).                            
                                                                                
       01 WS-COUNT.                                                             
          05 WS-PERSON-READ-TOTAL         PIC 9(06).                            
          05 WS-PROVIDER-READ-TOTAL       PIC 9(06).                            
          05 WS-VACCINE-READ-TOTAL        PIC 9(06).                            
          05 WS-PERSON-INSERT-TOTAL       PIC 9(06).                            
          05 WS-PROVIDER-INSERT-TOTAL     PIC 9(06).                            
          05 WS-VACCINE-INSERT-TOTAL      PIC 9(06).                            
          05 WS-DB2-ERROR-TOTAL           PIC 9(06).                            
                                                                                
       01 WS-AUSTRALIAN-DATE.                                                   
          05 WS-DD                        PIC X(02).                            
          05 FILLER                       PIC X(01) VALUE '.'.                  
          05 WS-MM                        PIC X(02).                            
          05 FILLER                       PIC X(01) VALUE '.'.                  
          05 WS-YYYY                      PIC X(04).                            
                                                                                
       01 WS-PERSON.                                                            
          05 WS-PERSON-ID                 PIC 9(06).                            
          05 WS-PERSON-NAME.                                                    
             10 WS-PERSON-FIRST-NAME      PIC X(20).                            
             10 WS-PERSON-SURNAME         PIC X(20).                            
          05 WS-PERSON-ADDRESS            PIC X(40).                            
          05 WS-PERSON-POSTCODE           PIC 9(04).                            
          05 WS-PERSON-SEX                PIC X(01).                            
          05 WS-PERSON-DOB.                                                     
             10 WS-PERSON-MONTH           PIC X(02).                            
             10 WS-PERSON-DAY             PIC X(02).                            
             10 WS-PERSON-YEAR            PIC X(04).                            
          05 WS-PERSON-STATE              PIC X(03).                            
          05 FILLER                       PIC X(31).                            
                                                                                
       01 WS-PROVIDER.                                                          
          05 WS-PROVIDER-ID               PIC 9(06).                            
          05 WS-PROVIDER-NAME.                                                  
             10 WS-PROVIDER-FIRST-NAME    PIC X(20).                            
             10 WS-PROVIDER-SURNAME       PIC X(20).                            
          05 WS-PROVIDER-ADDRESS          PIC X(40).                            
          05 WS-PROVIDER-POSTCODE         PIC 9(04).                            
          05 WS-PERSON-STATE              PIC X(03).                            
          05 FILLER                       PIC X(37).                            
                                                                                
       01 WS-VACCINE.                                                           
          05 WS-VACCINE-PROVIDER-ID       PIC 9(06).                            
          05 WS-VACCINE-PERSON-ID         PIC 9(06).                            
          05 WS-VACCINE-JAB-1-TYPE        PIC X(01).                            
          05 WS-VACCINE-JAB-1-DATE.                                             
             10 WS-VACCINE-JAB-1-MONTH    PIC X(02).                            
             10 WS-VACCINE-JAB-1-DAY      PIC X(02).                            
             10 WS-VACCINE-JAB-1-YEAR     PIC X(04).                            
          05 WS-VACCINE-JAB-2-TYPE        PIC X(01).                            
          05 WS-VACCINE-JAB-2-DATE.                                             
             10 WS-VACCINE-JAB-2-MONTH    PIC X(02).                            
             10 WS-VACCINE-JAB-2-DAY      PIC X(02).                            
             10 WS-VACCINE-JAB-2-YEAR     PIC X(04).                            
          05 FILLER                       PIC X(100).                           
                                                                                
       01 WS-PARM.                                                              
          05 WS-PARM-DELAY-LOW            PIC 9(08).                            
          05 FILLER                       PIC X(01).                            
          05 WS-PARM-DELAY-HIGH           PIC 9(08).                            
          05 FILLER                       PIC X(63).                            
                                                                                
      * PERSON DCLGEN                                                           
           EXEC SQL                                                             
             INCLUDE DGERISPE                                                   
           END-EXEC.                                                            
                                                                                
      * PROVIDER DCLGEN                                                         
           EXEC SQL                                                             
             INCLUDE DGERISPR                                                   
           END-EXEC.                                                            
                                                                                
      * VACCINATION DCLGEN                                                      
           EXEC SQL                                                             
             INCLUDE DGERISVA                                                   
           END-EXEC.                                                            
                                                                                
      * SQL communication area (SQLCA) to check return status                   
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
      *------------------                                                       
       PROCEDURE DIVISION.                                                      
      *------------------                                                       
           PERFORM 000-HOUSEKEEPING.                                            
           PERFORM 100-MAIN.                                                    
           PERFORM 900-CLOSE-FILES.                                             
           GOBACK.                                                              
                                                                                
       000-HOUSEKEEPING.                                                        
      * Initialization Routine                                                  
           INITIALIZE WS-PERSON.                                                
           INITIALIZE WS-PROVIDER.                                              
           INITIALIZE WS-VACCINE.                                               
           INITIALIZE WS-PARM.                                                  
           INITIALIZE WS-COUNT.                                                 
           PERFORM 300-OPEN-FILES.                                              
      * Priming Read                                                            
           PERFORM 400-READ-PERSON-FILE.                                        
           PERFORM 410-READ-PROVIDER-FILE.                                      
           PERFORM 420-READ-VACCINE-FILE.                                       
           PERFORM 430-READ-PARM-FILE.                                          
           MOVE WS-PARM-DELAY-LOW  TO LK-DELAY-LOW                              
           MOVE WS-PARM-DELAY-HIGH TO LK-DELAY-HIGH                             
      * Low and High for Random Delay                                           
           DISPLAY 'RANDOM LOW :' LK-DELAY-LOW                                  
                   ' RANDOM HIGH :' LK-DELAY-HIGH.                              
                                                                                
       100-MAIN.                                                                
      * Process PERSON and PROVIDER                                             
           PERFORM UNTIL PROVIDER-EOF AND PERSON-EOF                            
           IF NOT PERSON-EOF THEN                                               
             PERFORM 520-PERSON-INSERT                                          
           END-IF                                                               
           IF NOT PROVIDER-EOF THEN                                             
             PERFORM 530-PROVIDER-INSERT                                        
           END-IF                                                               
           END-PERFORM                                                          
      * Process VACCINE                                                         
           PERFORM 540-VACCINE-INSERT UNTIL VACCINE-EOF                         
      * Display total number of records READ for each file                      
           DISPLAY 'PERSON READ TOTAL :' WS-PERSON-READ-TOTAL                   
                   ' PROVIDER READ TOTAL :' WS-PROVIDER-READ-TOTAL              
                   ' VACCINE READ TOTAL :' WS-VACCINE-READ-TOTAL                
      * Display total number of records INSERTED for each file                  
           DISPLAY 'PERSON INSERT TOTAL :' WS-PERSON-INSERT-TOTAL               
                   ' PROVIDER INSERT TOTAL :' WS-PROVIDER-INSERT-TOTAL          
                   ' VACCINE INSERT TOTAL :' WS-VACCINE-INSERT-TOTAL            
           DISPLAY 'DB2 ERROR TOTAL :' WS-DB2-ERROR-TOTAL.                      
                                                                                
       300-OPEN-FILES.                                                          
           OPEN INPUT ERIS-PERSON-FILE.                                         
           OPEN INPUT ERIS-PROVIDER-FILE.                                       
           OPEN INPUT ERIS-VACCINE-FILE.                                        
           OPEN INPUT ERIS-PARM-FILE.                                           
                                                                                
       400-READ-PERSON-FILE.                                                    
           READ ERIS-PERSON-FILE INTO WS-PERSON                                 
      * Set AT END Switch                                                       
             AT END MOVE 'Y' TO ERIS-PERSON-EOF                                 
           END-READ.                                                            
                                                                                
       410-READ-PROVIDER-FILE.                                                  
           READ ERIS-PROVIDER-FILE INTO WS-PROVIDER                             
      * Set AT END Switch                                                       
             AT END MOVE 'Y' TO ERIS-PROVIDER-EOF                               
           END-READ.                                                            
                                                                                
       420-READ-VACCINE-FILE.                                                   
           READ ERIS-VACCINE-FILE INTO WS-VACCINE                               
      * Set AT END Switch                                                       
             AT END MOVE 'Y' TO ERIS-VACCINE-EOF                                
           END-READ.                                                            
                                                                                
       430-READ-PARM-FILE.                                                      
           READ ERIS-PARM-FILE INTO WS-PARM                                     
      * Set AT END Switch                                                       
             AT END MOVE 'Y' TO ERIS-PARM-EOF                                   
           END-READ.                                                            
                                                                                
       500-RANDOM-DELAY.                                                        
      * Andrew Mattingly Tools to return random number and delay                
           MOVE 'AMRANDOM' TO LK-DELAY-DYNAM-FUNC                               
      * Generate random number                                                  
           CALL LK-DELAY-DYNAM-FUNC USING LK-DELAY-LOW                          
                                          LK-DELAY-HIGH                         
                                          LK-DELAY-RANDOM.                      
           MOVE 'AMDELAY' TO LK-DELAY-DYNAM-FUNC                                
      * Delay in milliseconds                                                   
           CALL LK-DELAY-DYNAM-FUNC USING LK-DELAY-RANDOM.                      
                                                                                
       510-VALIDATE-STATE.                                                      
      * Determine the STATE from the POSTCODE                                   
           EVALUATE WS-POSTCODE                                                 
      * NSW                                                                     
           WHEN 1000 THRU 2599                                                  
             MOVE 'NSW' TO WS-STATE                                             
           WHEN 2619 THRU 2899                                                  
             MOVE 'NSW' TO WS-STATE                                             
           WHEN 2921 THRU 2999                                                  
             MOVE 'NSW' TO WS-STATE                                             
      * ACT                                                                     
           WHEN 0001 THRU 0299                                                  
             MOVE 'ACT' TO WS-STATE                                             
           WHEN 2600 THRU 2618                                                  
             MOVE 'ACT' TO WS-STATE                                             
           WHEN 2900 THRU 2920                                                  
             MOVE 'ACT' TO WS-STATE                                             
      * VIC                                                                     
           WHEN 3000 THRU 3999                                                  
             MOVE 'VIC' TO WS-STATE                                             
           WHEN 8000 THRU 8999                                                  
             MOVE 'VIC' TO WS-STATE                                             
      * QLD                                                                     
           WHEN 4000 THRU 4999                                                  
             MOVE 'QLD' TO WS-STATE                                             
           WHEN 9000 THRU 9999                                                  
             MOVE 'QLD' TO WS-STATE                                             
      * SA                                                                      
           WHEN 5000 THRU 5999                                                  
             MOVE 'SA' TO WS-STATE                                              
      * WA                                                                      
           WHEN 6000 THRU 6999                                                  
             MOVE 'WA' TO WS-STATE                                              
      * TAS                                                                     
           WHEN 7000 THRU 7999                                                  
             MOVE 'TAS' TO WS-STATE                                             
      * NT                                                                      
           WHEN 0300 THRU 0999                                                  
             MOVE 'NT' TO WS-STATE                                              
           END-EVALUATE.                                                        
                                                                                
       520-PERSON-INSERT.                                                       
           PERFORM 500-RANDOM-DELAY                                             
           MOVE WS-PERSON-ID           TO PERSON-ID IN DCLPERSON                
           MOVE WS-PERSON-FIRST-NAME   TO PERSON-FIRST-NAME                     
           MOVE WS-PERSON-SURNAME      TO PERSON-SURNAME                        
           MOVE WS-PERSON-ADDRESS      TO PERSON-ADDRESS                        
           MOVE WS-PERSON-POSTCODE     TO PERSON-POSTCODE                       
           MOVE WS-PERSON-SEX          TO PERSON-SEX                            
      * Fromat date into DD.MM.YYYY                                             
           MOVE WS-PERSON-DAY          TO WS-DD                                 
           MOVE WS-PERSON-MONTH        TO WS-MM                                 
           MOVE WS-PERSON-YEAR         TO WS-YYYY                               
           MOVE WS-AUSTRALIAN-DATE     TO PERSON-DOB                            
      * Determine the STATE from the POSTCODE                                   
           MOVE WS-PERSON-POSTCODE     TO WS-POSTCODE                           
           PERFORM 510-VALIDATE-STATE                                           
           MOVE WS-STATE               TO PERSON-STATE                          
      * SQL insert - one row only                                               
           EXEC SQL                                                             
             INSERT INTO ERIS.PERSON                                            
               (                                                                
                PERSON_ID,                                                      
                PERSON_FIRST_NAME,                                              
                PERSON_SURNAME,                                                 
                PERSON_ADDRESS,                                                 
                PERSON_POSTCODE,                                                
                PERSON_STATE,                                                   
                PERSON_SEX,                                                     
                PERSON_DOB                                                      
               )                                                                
             VALUES                                                             
               (                                                                
                :DCLPERSON.PERSON-ID,                                           
                :DCLPERSON.PERSON-FIRST-NAME,                                   
                :DCLPERSON.PERSON-SURNAME,                                      
                :DCLPERSON.PERSON-ADDRESS,                                      
                :DCLPERSON.PERSON-POSTCODE,                                     
                :DCLPERSON.PERSON-STATE,                                        
                :DCLPERSON.PERSON-SEX,                                          
                :DCLPERSON.PERSON-DOB                                           
               )                                                                
           END-EXEC                                                             
      * Check SQL returncode                                                    
           PERFORM 800-SQL-RETURN-CODE                                          
      * Check SQL returncode                                                    
           PERFORM 800-SQL-RETURN-CODE                                          
      * Total number of successful records processed                            
           IF SQLCODE EQUAL TO ZERO                                             
             ADD 1 TO WS-PERSON-INSERT-TOTAL                                    
           ELSE                                                                 
             DISPLAY 'ERROR PERSON RECORD :' WS-PERSON                          
           END-IF                                                               
      * Commit insert                                                           
           EXEC SQL                                                             
             COMMIT WORK                                                        
           END-EXEC                                                             
      * Check SQL returncode                                                    
           PERFORM 800-SQL-RETURN-CODE                                          
           ADD 1 TO WS-PERSON-READ-TOTAL                                        
           PERFORM 400-READ-PERSON-FILE.                                        
                                                                                
       530-PROVIDER-INSERT.                                                     
           PERFORM 500-RANDOM-DELAY                                             
           MOVE WS-PROVIDER-ID         TO PROVIDER-ID IN DCLPROVIDER            
           MOVE WS-PROVIDER-FIRST-NAME TO PROVIDER-FIRST-NAME                   
           MOVE WS-PROVIDER-SURNAME    TO PROVIDER-SURNAME                      
           MOVE WS-PROVIDER-ADDRESS    TO PROVIDER-ADDRESS                      
           MOVE WS-PROVIDER-POSTCODE   TO PROVIDER-POSTCODE                     
           MOVE WS-PROVIDER-POSTCODE   TO WS-POSTCODE                           
      * Determine the STATE from the POSTCODE                                   
           PERFORM 510-VALIDATE-STATE                                           
           MOVE WS-STATE               TO PROVIDER-STATE                        
      * SQL insert - one row only                                               
           EXEC SQL                                                             
             INSERT INTO ERIS.PROVIDER                                          
               (                                                                
                PROVIDER_ID,                                                    
                PROVIDER_FIRST_NAME,                                            
                PROVIDER_SURNAME,                                               
                PROVIDER_ADDRESS,                                               
                PROVIDER_POSTCODE,                                              
                PROVIDER_STATE                                                  
               )                                                                
             VALUES                                                             
               (                                                                
                :DCLPROVIDER.PROVIDER-ID,                                       
                :DCLPROVIDER.PROVIDER-FIRST-NAME,                               
                :DCLPROVIDER.PROVIDER-SURNAME,                                  
                :DCLPROVIDER.PROVIDER-ADDRESS,                                  
                :DCLPROVIDER.PROVIDER-POSTCODE,                                 
                :DCLPROVIDER.PROVIDER-STATE                                     
               )                                                                
           END-EXEC                                                             
      * Check SQL returncode                                                    
           PERFORM 800-SQL-RETURN-CODE                                          
      * Check SQL returncode                                                    
           PERFORM 800-SQL-RETURN-CODE                                          
      * Total number of successful records processed                            
           IF SQLCODE EQUAL TO ZERO                                             
             ADD 1 TO WS-PROVIDER-INSERT-TOTAL                                  
           ELSE                                                                 
             DISPLAY 'ERROR PROVIDER RECORD :' WS-PROVIDER                      
           END-IF                                                               
      * Commit insert                                                           
           EXEC SQL                                                             
             COMMIT WORK                                                        
           END-EXEC                                                             
      * Check SQL returncode                                                    
           PERFORM 800-SQL-RETURN-CODE                                          
           ADD 1 TO WS-PROVIDER-READ-TOTAL                                      
           PERFORM 410-READ-PROVIDER-FILE.                                      
                                                                                
       540-VACCINE-INSERT.                                                      
           PERFORM 500-RANDOM-DELAY                                             
           MOVE WS-VACCINE-PERSON-ID   TO PERSON-ID IN DCLVACCINATION           
           MOVE WS-VACCINE-PROVIDER-ID TO PROVIDER-ID IN DCLVACCINATION         
           MOVE WS-VACCINE-JAB-1-TYPE  TO JAB-1-TYPE                            
      * Fromat date into DD.MM.YYYY                                             
           MOVE WS-VACCINE-JAB-1-DAY   TO WS-DD                                 
           MOVE WS-VACCINE-JAB-1-MONTH TO WS-MM                                 
           MOVE WS-VACCINE-JAB-1-YEAR  TO WS-YYYY                               
           MOVE WS-AUSTRALIAN-DATE     TO JAB-1-DATE                            
      * Test if JAB-2 is empty                                                  
           IF WS-VACCINE-JAB-2-TYPE NOT EQUAL TO SPACE                          
             MOVE WS-VACCINE-JAB-2-TYPE  TO JAB-2-TYPE                          
      * Fromat date into DD.MM.YYYY                                             
             MOVE WS-VACCINE-JAB-2-DAY   TO WS-DD                               
             MOVE WS-VACCINE-JAB-2-MONTH TO WS-MM                               
             MOVE WS-VACCINE-JAB-2-YEAR  TO WS-YYYY                             
             MOVE WS-AUSTRALIAN-DATE     TO JAB-2-DATE                          
      * SQL insert - one row only JAB 2 complete                                
             EXEC SQL                                                           
               INSERT INTO ERIS.VACCINATION                                     
                 (                                                              
                  PERSON_ID,                                                    
                  PROVIDER_ID,                                                  
                  JAB_1_TYPE,                                                   
                  JAB_1_DATE,                                                   
                  JAB_2_TYPE,                                                   
                  JAB_2_DATE                                                    
                 )                                                              
               VALUES                                                           
                 (                                                              
                  :DCLVACCINATION.PERSON-ID,                                    
                  :DCLVACCINATION.PROVIDER-ID,                                  
                  :DCLVACCINATION.JAB-1-TYPE,                                   
                  :DCLVACCINATION.JAB-1-DATE,                                   
                  :DCLVACCINATION.JAB-2-TYPE,                                   
                  :DCLVACCINATION.JAB-2-DATE                                    
                 )                                                              
             END-EXEC                                                           
           ELSE                                                                 
      * SQL insert - one row only JAB-2 NULL                                    
             EXEC SQL                                                           
               INSERT INTO ERIS.VACCINATION                                     
                 (                                                              
                  PERSON_ID,                                                    
                  PROVIDER_ID,                                                  
                  JAB_1_TYPE,                                                   
                  JAB_1_DATE                                                    
                 )                                                              
               VALUES                                                           
                 (                                                              
                  :DCLVACCINATION.PERSON-ID,                                    
                  :DCLVACCINATION.PROVIDER-ID,                                  
                  :DCLVACCINATION.JAB-1-TYPE,                                   
                  :DCLVACCINATION.JAB-1-DATE                                    
                 )                                                              
             END-EXEC                                                           
           END-IF                                                               
      * Check SQL returncode                                                    
           PERFORM 800-SQL-RETURN-CODE                                          
      * Total number of successful records processed                            
           IF SQLCODE EQUAL TO ZERO                                             
             ADD 1 TO WS-VACCINE-INSERT-TOTAL                                   
           ELSE                                                                 
             DISPLAY 'ERROR VACCINE RECORD :' WS-VACCINE                        
           END-IF                                                               
      * Commit insert                                                           
           EXEC SQL                                                             
             COMMIT WORK                                                        
           END-EXEC                                                             
      * Check SQL returncode                                                    
           PERFORM 800-SQL-RETURN-CODE                                          
           ADD 1 TO WS-VACCINE-READ-TOTAL                                       
           PERFORM 420-READ-VACCINE-FILE.                                       
                                                                                
       800-SQL-RETURN-CODE.                                                     
           IF SQLCODE NOT EQUAL TO ZERO                                         
             DISPLAY 'SQL ERROR '                                               
                     ' SQLCODE :' SQLCODE                                       
                     ' SQLSTATE  :' SQLSTATE                                    
             ADD 1 TO WS-DB2-ERROR-TOTAL                                        
           END-IF.                                                              
                                                                                
       900-CLOSE-FILES.                                                         
           CLOSE ERIS-PERSON-FILE,                                              
                 ERIS-PROVIDER-FILE,                                            
                 ERIS-VACCINE-FILE,                                             
                 ERIS-PARM-FILE.                                                
                                                                                
